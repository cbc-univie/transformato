* SB June 2020
* Attempt to read OpenMM stuff into charmm
*

if @?method .eq. 0 set method 1

! method 1 : VSWI 10 12
! method 2 : VSWI 12 12
! method 3 : VFWI 10 12

stream read_rtf_par.str

! try to read ligand specific RTFs/PARAm files, however named
if mol .eq. TOLUENE then
read rtf card append name unl_g.rtf
read para card append flex name unl.prm
endif
if mol .eq. TOLUENE goto done

if mol .eq. NEOPENTANE then
read rtf card append name unl_g.rtf
read para card append flex name unl.prm
endif
if mol .eq. NEOPENTANE goto done

if mol .eq. 2-CPI then
read rtf card append name unl_g.rtf
read para card append flex name unl.prm
endif
if mol .eq. 2-CPI goto done

if mol .eq. 7-CPI then
read rtf card append name unl_g.rtf
read para card append flex name unl.prm
endif
if mol .eq. 7-CPI goto done

if mol .eq. METHANE then
read rtf card append name lig_g.rtf
read para card append flex name lig.prm
endif
if mol .eq. METHANE goto done

! all others (so far) seem to be named unk*
read rtf card append name unk_g.rtf
read para card append flex name unk.prm

label done

read rtf card append name dummy_atom_definitions.rtf
read para card append flex name dummy_parameters.prm

read psf card name lig_in_waterbox.psf
read coor card name lig_in_waterbox.crd

cons fix sele segi SOLV end
mini sd nstep 10
mini abnr nstep 50
cons fix sele none end

set bxl 30.0

MMFP
GEO rcm sphere -
    Xref 0.0 Yref 0.0 Zref 0.0 XDIR 1.0 YDIR 1.0 ZDIR 1.0 -
    harmonic FORCE 1.0 select .not. ( hydrogen .or. resname TIP3 ) end
END

!shak bonh para fast sele segi WAT end
shak bonh para fast sele segi SOLV end

set ljswi VSWI
if @method .eq. 3 set ljswi VFSW
set ctofnb 12.
set ctonnb 10.
if @method .eq. 2 set ctonnb 12. ! hard cutoff
set cutnb  14.
set cutim  @cutnb
crystal define cubic @bxl @bxl @bxl 90.00 90.00 90.00
crystal build Noper 0 cutoff @cutim
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

nbonds ctonnb @ctonnb ctofnb @ctofnb cutnb @cutnb cutim @cutim -
  atom shif vatom @ljswi -
  inbfrq -1 imgfrq -1 -
  ewald pmew kappa 0.34 spline order 6 fftx 32 ffty 32 fftz 32

energy

mini sd nstep 10

! from charmm-gui scripts -- CPT dynamics
! NPT dynamics:
! you can change
! nstep  : number of MD steps
! nprint : print-out frequency
! nsavc  : the trajectory saving frequency
!

! estimate Pmass from SYSmass (total system mass)
! [there could be problems with exreme values, such as  Pmass << SYSmass or Pmass >> SYSmass
scalar mass stat
calc Pmass = int ( ?stot  /  50.0 )

if @?rand .eq. 0 set rand 1
calc r2 = 2*@rand
calc r3 = 3*@rand
calc r4 = 79*@rand

energy
domdec gpu only
energy

set nstep = 200000
set temp = 300.0

open write unit 12 card name equilbox_@{method}.@{rand}.rst

DYNA CPT leap start time 0.001 nstep @nstep -
     nprint 1000 iprfrq 1000 ntrfrq 1000 -
     iunread -1 iunwri 12 iuncrd -1 iunvel -1 kunit -1 -
     nsavc 50000 nsavv 0 iseed @rand @r2 @r3 @r4 -
     PCONSTANT pref   1.0  pmass @Pmass  pgamma   20.0 -
     HOOVER reft @temp  tmass 2000.0  tbath   @temp  firstt @temp -
     echeck 0

stop
